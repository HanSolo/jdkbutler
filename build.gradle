plugins {
    id 'idea'
    id 'java-library'
    id 'application'
    id 'org.kordamp.gradle.java-project'
    id 'org.openjfx.javafxplugin'
    id 'biz.aQute.bnd.builder'
}

if (!project.hasProperty('bintrayUsername'))  ext.bintrayUsername  = '**undefined**'
if (!project.hasProperty('bintrayApiKey'))    ext.bintrayApiKey    = '**undefined**'
if (!project.hasProperty('sonatypeUsername')) ext.sonatypeUsername = '**undefined**'
if (!project.hasProperty('sonatypePassword')) ext.sonatypePassword = '**undefined**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'jdkbutler'
        description   = 'jdkbutler is a little demo project that shows how to create custom controls in JavaFX'
        vendor        = 'Hansolo'
        inceptionYear = '2021'
        tags          = ['javafx', 'java']

        links {
            website      = 'https://github.com/HanSolo/jdkbutler/wiki'
            issueTracker = 'https://github.com/HanSolo/jdkbutler/issues'
            scm          = 'https://github.com/HanSolo/jdkbutler.git'
        }

        people {
            person {
                id    = 'HanSolo'
                name  = 'Gerrit Grunwald'
                url   = 'https://harmoniccode.blogspot.com/'
                roles = ['developer']
            }
        }

        credentials {
            sonatype {
                username = project.sonatypeUsername
                password = project.sonatypePassword
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        enabled      = true
        userOrg      = 'hansolo'
        repo         = 'jdkbutler'
        name         = rootProject.name
        publish      = config.release
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }

    docs {
        javadoc {
            autoLinks {
                enabled = false
            }
        }
    }
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

repositories {
    jcenter()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.foojay.api:discoclient:1.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
    implementation 'javax.validation:validation-api:2.0.1.Final'
}

mainClassName = '$moduleName/eu.hansolo.fx.jdkbutler.MainPreloader'

java {
    modularity.inferModulePath.set(true)
}

javafx {
    version = javafxVersion
    modules = [ 'javafx.base', 'javafx.graphics', 'javafx.controls' ]
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Implementation-Title': 'JDKButler',
                'Bundle-Name': project.name,
                'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description': config.info.description,
                'Bundle-SymbolicName': 'eu.hansolo.fx.jdkbutler',
                'Export-Package': 'eu.hansolo.fx.jdkbutler,eu.hansolo.fx.jdkbutler.tools,eu.hansolo.fx.jdkbutler.controls',
                'Main-Class': 'eu.hansolo.fx.jdkbutler.Launcher'
        )
    }
}


// start the demo from gradle
task main(type: JavaExec) {
    main = "eu.hansolo.fx.jdkbutler.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}


publishing {
    publications {
        main(MavenPublication) {
            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }
        }
    }
}

// Fix problems with loading resources
sourceSets {
    main {
        output.setResourcesDir(java.outputDir)
    }
}